import "pkg:/source/UIBuilder/view/ScreenVM.bs"
import "pkg:/source/lib/ViewUtils.bs"

class LogoutScreenVM extends zapp.uib.ScreenVM

  public isButtonVisible = true
  public isShowingLoadingIndicator = false

  'styles
  public titleStyle = invalid
  public titleText = "Are you sure you want to sign out?"
  public buttonText = "Sign out"
  public buttonStyle = invalid
  public backgroundColor = "#000000ff"
  public textPosition = [0, 310]
  public buttonPosition = [0, 310]
  public textWidth = 1920
  private isLoggedOut = false
  private signedOutButtonText = ""
  private signedOutTitle = ""
  private providerId = invalid
  private authTask

  public function new(riversJson)
    super(riversJson, "LogoutScreenVM")

    m.focusId = "logoutButton"
    m.processStyles(m.riversJson.styles)

  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Overridden
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  public override function createContent()
    m.log.info("createContent is overridden in this screen")
  end function

  public override function load(doNotLoadIfReady = false) as boolean
    return true
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ styles
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  function processStyles(styles)
    m.log.info("converting rivers json styles into a format our UI controls can consume")

    m.titleStyle = zapp.viewUtils.makeUIBTextStyle(styles, "title")
    m.titleText = m.titleStyle.text
    m.backgroundColor = zapp.viewUtils.transposeUIBColor(styles.background_color)

    m.buttonStyle = zapp.viewUtils.makeUIBButtonStyle(styles, "logout_button", "custom")
    m.buttonStyle.normal.delete("text")
    m.textPosition = zapp.viewUtils.makeUIBPosition(styles, "text", 0, 310)
    m.buttonPosition = zapp.viewUtils.makeUIBPosition(styles, "button", 400, 600)
    m.textWidth = zapp.viewUtils.getUIBInt(styles, "text_w", 1920)
    m.signedOutButtonText = styles.signed_out_button_text
    m.signedOutTitle = styles.signed_out_text
    m.providerId = mc.getPath(styles, "provider_id", invalid)
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ callbacks
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  public function onLogoutButtonSelected(value)
    m.log.method("onLogoutButtonSelected")
    if m.isLoggedOut
      m.restartApp()
    else
      m.logout()
    end if
  end function

  function logout()
    m.log.method("logout")

    if m.providerId = invalid
      defaultId = m.getSessionStoreValue("all_zapp_pipes_providers", "default_auth_plugin_id")
    else
      defaultId = m.providerId
    end if

    analyticsManager = m.getInstance("analyticsManager")
    analyticsManager@.trackEvent("Signed Out", "page_loaded", { "provider": defaultId })

    if m.providerId = invalid or m.providerId.trim() = ""
      m.log.info("logging out all providers")
      m.authTask = m.createAuthTask("execLogOutAll", invalid, m.onLogoutComplete)
    else
      m.log.info("logging out with specific provider", m.providerId)
      m.authTask = m.createAuthTask("execLogOut", m.providerId, m.onLogoutComplete)
    end if

    m.isLoggedOut = true
    m.isShowingLoadingIndicator = true
    m.isButtonVisible = false
  end function

  function onLogoutComplete(result)
    m.log.method("onLogoutComplete")
    m.isShowingLoadingIndicator = false
    m.isButtonVisible = true
    m.buttonText = m.signedOutButtonText
    m.titleText = m.signedOutTitle
    m.setFocus(m.getMField("top"))
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Key Handling
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  public function isCapturingAnyKeyPress(key, press)
    'do not anyone escape this screen!
    return true
  end function

  public function onKeyPressBack() as boolean

    if not m.isLoggedOut
      m.pop()
    else
      m.log.info("cannot go back when logout is in process")
    end if

    return true
  end function

  public override function onKeyPressUp() as boolean
    return true
  end function

end class
