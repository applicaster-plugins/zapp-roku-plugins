import "pkg:/source/lib/ViewUtils.bs"
import "pkg:/source/roku_modules/maestro/core/Utils.brs"
import "pkg:/source/UIBuilder/view/ComponentVM.bs"
import "pkg:/source/zapp-controls/CollectionRowUtils.bs"

class FullScreenSearchVM extends zapp.uib.ComponentVM

  public results = invalid
  public isGridVisible = false
  public isNoSearchLabelVisible = false
  public isNotFoundLabelVisible = false

  public keyboardStyle = {}
  public gridStyle = {}

  public jumpToItem = 0
  public searchText = ""

  private searchDelayTask = invalid
  private loadNextOffset = 2
  private isPaginationSupported = true
  private isMoreAvailable = false
  private loadMoreTask = invalid
  private numItems = 0
  private numRows = 0
  private numCols = 4
  private nextUrl = invalid
  private minCharacters = 3
  private searchQueryName = "q"
  private isShowingContent = false
  private direction = 1
  private lastRowIndex = -1
  private rowTarget = invalid

  public function new(riversJson)
    super(riversJson, "FullScreenSearchVM")
    m.focusId = "keyboard"
    m.processStyles(riversJson.styles)
    m.setGridVisible(false)

    m.rowTarget = riversJson.data.target

    if zapp.viewUtils.getUIBBool(riversJson.styles, "override_target_screen", false)
      m.rowTarget = mc.getPath(riversJson, "data.target_screen")
      m.log.warn("Using overridden screen name for target!", m.rowTarget)
    end if

  end function

  function processStyles(styles)
    m.loadDepth = zapp.viewUtils.getUIBInt(styles, "data_depth", 0)
    m.gridStyle = m.getGridStyle(styles, m.style)
    m.loadNextOffset = zapp.viewUtils.getUIBInt(styles, "load_next_offset", 2)

    m.keyboardStyle = {
      translation: zapp.viewUtils.makeUIBPosition(styles, "keyboard", 300, 500)
      keyColor: zapp.viewUtils.transposeUIBColor(styles.key_color)
      keyboardBitmapUri: zapp.viewUtils.getPosterUri(styles.keyboard_bitmap_uri)
      focusBitmapUri: zapp.viewUtils.getPosterUri(styles.keyboard_focus_bitmap_uri)
      lowerCase: zapp.viewUtils.getUIBBool(styles, "keyboard_lower_case", false)
    }
    keyboard = m.getMField("keyboard")
    palettes = createObject("roSGNode", "RSGPalette")
    palettes.colors = {
      primaryTextColor: zapp.viewUtils.transposeUIBColor(styles.key_color)
      focusColor: zapp.viewUtils.transposeUIBColor(styles.focused_key_color)
    }
    keyboard.keyGrid.palette = palettes
    keyboard.textEditBox.textColor = zapp.viewUtils.transposeUIBColor(styles.key_color)

    m.minCharacters = zapp.viewUtils.getUIBInt(styles, "search_min_chars", 3)
    m.searchQueryName = mc.getPath(styles, "search_query_name", "q")
  end function

  private function getGridStyle(styles, cellStyle)
    if cellStyle <> invalid
      style = zapp.collectionRowUtils.getRowListStyle(styles, mc.getAA(m.style, "cell.normal"))
      style.itemSize = style.rowItemSize
      style.itemSpacing = zapp.viewUtils.makeUIBSize(styles, "grid_spacing", 44, 20)
      style["numColumns"] = zapp.viewUtils.getUIBInt(styles, "number_of_cols", 2)
      m.numCols = style["numColumns"]
    else
      style = {}
    end if

    style.translation = zapp.viewUtils.makeUIBPosition(styles, "grid", 900, 500)

    return style
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ overridden
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  public override function load()
    m.state = "ready"
    m.isLoading = false
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ callbacks
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  public function onSelectItem(index)
    m.log.info("onSelecteItem", index)

    item = m.results.getChild(index)

    if item <> invalid and item.itemType <> "placeholder"
      m.log.info("selected item: ", item.id)
      m.executeContentSelection(m.createSelectionFromContent(item, m.results))
      m.isShowingContent = true
    else
      m.log.warn("selected non-selectable item")
    end if

  end function

  public function onFocusRow(index)
    m.log.info("onFocusRow", index)

    if index < m.lastRowIndex
      m.direction = -1
    else if index > m.lastRowIndex
      m.direction = 1
    end if

    m.lastRowIndex = index

    if m.canLoadMore()
      m.log.info("loading more")
      m.loadMore()
    end if
  end function

  public function onSearchTextChange(text)
    m.log.info("text is now", text, "waiting before searching")

    m.searchText = text
    m.cancelWaitTimer(m.searchDelayTask)
    m.cancelTask(m.loadDataTask)

    if len(m.searchText.trim()) >= m.minCharacters
      m.searchDelayTask = m.waitAFrame(m.onSearchDelayComplete, 0.5)
    else
      m.log.verbose("too few chars. ignoring")
      m.setGridVisible(false)
      m.isLoading = false
    end if

  end function

  public function onSearchDelayComplete()
    m.log.method("onSearchDelayComplet")
    m.searchDelayTask = invalid
    m.log.verbose("search debounce complete, exeucting search with", m.searchText)
    m.performSearch(m.searchText)
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ search api invocation
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private function performSearch(text)
    m.log.method("performSearch")

    m.isLoading = true
    m.setGridVisible(true)
    m.toggleLoadingDataTask(true, { "search": text.trim().encodeUriComponent() }, m.loadDepth)

  end function

  public override function onFeedResult(result)
    m.log.method("onFeedResult")

    m.toggleLoadingDataTask(false)
    m.isLoading = false
    results = result.data
    if result.isOk and results <> invalid
      m.setContentStyle(results)
      m.nextUrl = zapp.collectionRowUtils.getNextUrl(result.data.json)
      m.numItems = result.data.getChildCount()
      m.numRows = m.numItems / m.numCols
      m.feed = result.data
      m.isMoreAvailable = m.nextUrl <> invalid and m.nextUrl <> ""
    else
      results = createObject("roSGNode", "ZPEntry")
    end if

    mc.setOrAddNodeField(results, "target", m.rowTarget)

    m.results = results
    m.jumpToItem = 0
    m.setGridVisible(true)
  end function

  private function setGridVisible(isGridVisible)
    m.isGridVisible = isGridVisible and m.results.getChildCount() > 0
    m.isNotFoundLabelVisible = isGridVisible and m.results.getChildCount() = 0 and not m.isLoading
    m.isNoSearchLabelVisible = not isGridVisible and not m.isLoading

    if not m.isGridVisible
      m.results = createObject("roSGnode", "ZPEntry")
    end if

    if not m.isGridVisible and m.focusId = "grid"
      m.setFocusId("keyboard")
    end if
  end function

  private function setContentStyle(content)
    m.log.method("setContentStyle")

    if m.style <> invalid
      cellStyle = m.style.cell
      cellStyle.dataMap = m.style.dataMap
      cellStyle.delete("translation")
      mc.setOrAddNodeField(content, "_style", cellStyle)
      mc.setOrAddNodeField(content, "_styleId", m.style.id)
    else
      m.log.error("Cannot set content: there is no cell style set")
    end if

  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Private Methods
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  'TODO - pagination should move to a utility class
  private function loadMore()
    m.log.method("loadMore")
    m.state = "loading"

    m.loadMoreTask = m.executeloadTaskWithUrl(m.feed.id, m.onLoadMoreResponse, m.nextUrl, 0)
  end function

  private function onLoadMoreResponse(result)
    m.log.method("onLoadMoreResponse")

    if result.isOk
      m.setContentStyle(result.data)
      m.nextUrl = zapp.collectionRowUtils.getNextUrl(result.data.json)
      m.isMoreAvailable = m.nextUrl <> invalid and m.nextUrl <> ""
      m.numItems += result.data.getChildCount()
      m.numRows = m.numItems / m.numCols
      m.results.appendChildren(result.data.getChildren(-1, 0))
    end if
    m.state = "ready"
  end function

  private function canLoadMore()
    return m.results <> invalid and m.isPaginationSupported and m.direction = 1 and m.isMoreAvailable and m.state <> "loading" and m.lastRowIndex > m.numRows - m.loadNextOffset
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ keyboard
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  function onKeyPressRight() as boolean
    if m.focusId = "keyboard" and m.isGridVisible
      m.setFocusId("grid")
      return true
    end if

    return false
  end function

  function onKeyPressLeft() as boolean
    if m.focusId = "grid"
      m.setFocusId("keyboard")
      return true
    end if

    return false
  end function

  override function reset()
    m.jumpToItem = 0
    m.results = createObject("roSGNode", "ZPEntry")
    m.setGridVisible(false)
    m.searchText = ""
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ lifecycle
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  protected override function onHide()
    super.onHide()

    m.cancelWaitTimer(m.searchDelayTask)
    m.cancelTask(m.loadDataTask)
    m.loadDataTask = invalid
    m.searchDelayTask = invalid
  end function

  protected override function onShow() as void
    m.log.method("onShow")
    m.trackScreen()
    if not m.isShowingContent
      m.reset()
    end if

    m.isShowingContent = false
  end function

end class
