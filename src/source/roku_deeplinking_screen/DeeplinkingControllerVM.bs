import "pkg:/source/UIBuilder/view/ScreenVM.bs"
import "pkg:/source/lib/ViewUtils.bs"
import "../lib/DSP2DeeplinkingProcessor.bs"
import "pkg:/source/UIBuilder/view/utils/AuthUtil.bs"

class DeeplinkingControllerVM extends zapp.uib.ScreenVM

  public isLoadingIndicatorVisible = false
  public isHidingContent = true
  public currentScreen = invalid
  private isLiveDeeplink = false
  private isShowingPrehooks = false
  private isShowingParentScreen = false
  private isDeeplinkingApplied = false
  private deeplinkingTask = invalid
  private deeplinkingData = invalid
  private willExitOnShow = false
  private parentScreen as mc.types.node
  private dsp2Processor = new zapp.DSP2DeeplinkingProcessor()
  'bs:disable-next-line
  private authUtil = new zapp.view.utils.AuthUtil(m)

  @inject("deepLinkIOC")
  private deepLinkIOC

  public screenIdsByName = {}

  public function new(riversJson)
    super(riversJson, "DeeplinkingControllerVM")
    m.focusId = "screenContainer"
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ overridden
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  public override function createContent()
  end function

  public override function load() as boolean
    m.log.info("deeplink.load")
    if m.isDeeplinkingApplied
      if not m.isShowingParentScreen
        m.log.info("the screen has appeared again, probably from a video player; but has no parent screen, exiting from deeplinking")
      else
        m.log.info("deeplinking screen is loaded - not loading again")
      end if
      return false
    else
      m.log.info("toggling deeplink task")
      args = m.getInstance("deeplinkingArgs")
      m.isLiveDeeplink = args.isLiveDeeplink = true
      m.toggleDeeplinkingTask(true, args)
    end if

    return true
  end function


  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ prehook support
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private function togglePrehookController(isActive, controller = invalid)
    m.log.method("togglePrehookController", isActive)
    m.isLoadingIndicatorVisible = false

    m.unobserveNodeField(m.currentScreen, "state", m.onPrehookControllerStateChange)

    if isActive
      m.observeNodeField(controller, "state", m.onPrehookControllerStateChange, "value")
      m.currentScreen = controller
      m.setFocus(m.currentScreen)
    else
      m.currentScreen = invalid
    end if

    m.isShowingPrehooks = m.currentScreen <> invalid
  end function

  private function onPrehookControllerStateChange(state)
    m.log.info("prehook state changed", state)

    if state = "success"
      ' m.currentScreen = invalid
      m.log.info("prehook successfully finished - continuing")
      m.toggleLoadingIndicator(true)
      m.showParentScreen()

      queryArgs = m.deepLinkIOC.queryArgs
      m.dsp2Processor.executeFeedLocatorRequest(queryArgs)
    else if state = "error" or state = "back"
      ' m.currentScreen = invalid
      m.dismissDeeplinkingController()
    end if
  end function

  private function getPrehookController(screen as mc.types.node)
    m.log.info("getPrehookController")
    prehooks = m.getLoginScreenPrehook()

    if prehooks <> invalid and prehooks.count() > 0 and asBoolean(m.deepLinkIOC.isStillInDeeplinkMode)
      m.log.info("this deeplink controller has prehooks")
      prehookScreen = m.createInstance("ScreenHookController", invalid, "prehooksController")
      prehookScreen.hooks = prehooks
      prehookScreen.targetScreen = invalid
      prehookScreen.isManagingScreensOnCompletion = false
      return prehookScreen
    else
      return invalid
    end if

  end function

  private function getLoginScreenPrehook()
    prehook = m.authUtil.getAuthProvider()
    screenId = m.getScreenIdByPluginType(prehook.id)
    prehooks = [{
        identifier: asString(prehook.json.identifier)
        type: asString(prehook.json.type)
        screen_id: screenId
        weight: 1
      }
    ]
    return prehooks
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Deeplinking data retrieval
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  public function toggleDeeplinkingTask(isActive, args = invalid)
    m.log.method("toggleDeeplinkingTask", isActive, args)
    m.toggleLoadingIndicator(isActive)

    m.unobserveNodeField(m.deeplinkingTask, "deeplinkingData", m.onDeeplinkingTaskResult)
    m.deeplinkingTask = invalid

    if isActive
      m.log.info("starting deeplinking task")
      m.deeplinkingTask = m.createInstance("DeeplinkingTask", invalid, "deeplinkingTask")
      m.observe(m.deeplinkingTask.deeplinkingData, m.onDeeplinkingTaskResult, "value", true)
      m.observe(m.dsp2Processor.deeplinkIOC.isStillInDeeplinkMode, m.applyDeeplinkingForFeed, "value", true)

      m.deeplinkingTask.args = args
      m.deeplinkingTask.control = "run"
    end if
  end function

  public function onDeeplinkingTaskResult(deeplinkingData)
    m.log.method("onDeeplinkingTaskResult deeplinkingData", deeplinkingData)

    m.toggleDeeplinkingTask(false)

    if deeplinkingData.isValid
      m.setInstance("deeplinkingData", deeplinkingData)
      m.applyDeeplinking(deeplinkingData)
    else
      m.log.error("deeplinking task did not yield valid deep linking info")
      m.dismissDeeplinkingController()
    end if

  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ deeeplinking application
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  function applyDeeplinkingForFeed(deeplinkingData)
    if asBoolean(deeplinkingData)
      deeplinkingData = m.deeplinkingData
      if deeplinkingData <> invalid
        m.deepLinkIOC.isRequesting = false
        m.setInstance("skipOnExitFeedLocator", false)
        m.applyDeeplinking(deeplinkingData)
      end if
    end if
  end function

  function applyDeeplinking(deeplinkingData) as void
    m.log.method("applyDeeplinking - app is configured for deeplinking. Navigating to the appropriate view")
    if deeplinkingData.version = 2
      m.applyDeeplinkingForDSP2(deeplinkingData)
      m.log.method("applyDeeplinking - we are using deeplink version 2")
      return
    end if

    m.deeplinkingData = deeplinkingData
    deeplinkingSelection = m.getDeeplinkingSelectionWithData(deeplinkingData)

    if deeplinkingSelection = invalid
      m.log.error("Deep linking data could not be configured.")
      m.dismissDeeplinkingController()
      return
    end if

    m.log.info("Applying deep link for entry with entry ", mc.dv(deeplinkingData.entry), mc.dv(deeplinkingData.parentEntry))

    m.setInstance("deeplinkingSelection", deeplinkingSelection)


    if deeplinkingData.parentEntry <> invalid
      screen = zapp.uib.mixin.getMappedScreen(deeplinkingData.parentEntry,
      { "originEntry": deeplinkingData.parentEntry, "selection": deeplinkingSelection })

      if screen <> invalid and deeplinkingData.itemType <> "channel"
        m.isShowingParentScreen = m.deeplinkingData.isShowingParentScreen
        m.currentScreen = screen
        m.observeNodeField(screen, "state", m.onScreenStateChange)
        m.toggleNavMenuFocus(false)
        m.onContentStateChange(screen.state)
        m.observeNodeField(screen, "state", m.onContentStateChange)

        m.isDeeplinkingApplied = true
        m.signalAppLaunchComplete()
        if not m.isShowingParentScreen
          m.log.info("not actually showing parent screen - moving fuax focus")
          m.focusId = "logoPoster"
        end if
      end if
    else
      m.log.error("could not do deeplinking - exiting")
      m.exitFromDeeplinking()
    end if
  end function

  function applyDeeplinkingForDSP2(deeplinkingData) as void
    m.log.method("applyDeeplinking - app is configured for deeplinking. Navigating to the appropriate view")
    m.deeplinkingData = deeplinkingData
    deeplinkingSelection = m.getDeeplinkingSelectionWithData(deeplinkingData)

    if deeplinkingSelection = invalid
      m.log.error("Deep linking data could not be configured.")
      m.dismissDeeplinkingController()
      return
    end if

    m.log.info("Applying deep link for entry with entry ", mc.dv(deeplinkingData.entry), mc.dv(deeplinkingData.parentEntry))

    m.setInstance("deeplinkingSelection", deeplinkingSelection)
    skipOnExitFeedLocator = m.getInstance("skipOnExitFeedLocator")

    if asBoolean(skipOnExitFeedLocator)
      m.log.error("exiting feed locator entry")
      m.exitFromDeeplinking()
    else if deeplinkingData.parentEntry <> invalid
      m.parentScreen = m.getParentScreen(deeplinkingData, deeplinkingSelection)
      if asString(deeplinkingData.feed_locator) <> ""
        m.setInstance("skipOnExitFeedLocator", true)
      end if
      if m.shouldShowPrehook()
        m.log.info("this screen requires a prehook to be executed first. showing it now")
      else
        m.showParentScreen()
      end if
    else
      m.log.error("could not do deeplinking - exiting")
      m.exitFromDeeplinking()
    end if
  end function

  private function shouldShowPrehook()
    prehookController = m.getPrehookController(m.parentScreen)

    if prehookController <> invalid
      m.log.info("this deeplinking screen has a prehookController")
      m.togglePrehookController(true, prehookController)
      return true
    end if
    return false
  end function

  private function showParentScreen()
    m.log.info("showing parent screen")

    if m.parentScreen <> invalid and m.deeplinkingData.itemType <> "channel"
      m.isShowingParentScreen = m.deeplinkingData.isShowingParentScreen
      m.currentScreen = m.parentScreen
      m.observeNodeField(m.parentScreen, "state", m.onScreenStateChange)
      m.toggleNavMenuFocus(false)
      m.onContentStateChange(m.parentScreen.state)
      m.observeNodeField(m.parentScreen, "state", m.onContentStateChange)

      m.isDeeplinkingApplied = true
      m.signalAppLaunchComplete()
      if not m.isShowingParentScreen
        m.log.info("not actually showing parent m.parentScreen - moving fuax focus")
        m.focusId = "logoPoster"
      end if
    end if
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Private Methods
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private function getParentScreen(deeplinkingData as mc.types.node, deeplinkingSelection as mc.types.node)
    data = { "originEntry": deeplinkingData.parentEntry, "selection": deeplinkingSelection }
    screen = invalid
    if deeplinkingData.screen_id <> ""
      screen = zapp.uib.mixin.getScreen(deeplinkingData.screen_id, data)
      if screen = invalid
        m.log.error("could not get deeplinking screen with id", deeplinkingData.screen_id)
      end if
    end if

    'fallback to this
    screen = zapp.uib.mixin.getMappedScreen(deeplinkingData.parentEntry)
    return screen
  end function

  private function onScreenStateChange(state as string)
    if state = "error"
      m.unobserveNodeField(m.currentScreen, "state", m.onScreenStateChange)
      m.log.info("screen entered error state, exiting")
      m.exitFromDeeplinking()
    else if state = "ready"
      if m.isShowingParentScreen
        m.isHidingContent = false
      end if
    end if
  end function

  private function toggleLoadingIndicator(isActive)
    m.isLoadingIndicatorVisible = isActive
  end function

  private function dismissDeeplinkingController()
    m.toggleLoadingIndicator(true)

    if m.isLiveDeeplink or not m.isShowingPrehooks
      m.log.info("going to go back, if possible")
      m.exitFromDeeplinking()
    else
      m.log.info("cannot go back, coz this is an app launch deeplink, with a prehook - it can't be cancelled")
      m.exitApp()
    end if
  end function

  private function getDeeplinkingSelectionWithData(deeplinkingData)
    m.log.method("getDeeplinkingSelectionWithData")

    if deeplinkingData = invalid or deeplinkingData.isValid <> true
      m.log.error("received invalid deeplinkingData")
      return invalid
    end if

    selection = CreateObject("roSGNode", "Selection")

    content = invalid

    if deeplinkingData.entry <> invalid
      content = deeplinkingData.entry.clone(true)
    else if deeplinkingData.parentEntry <> invalid
      content = deeplinkingData.parentEntry.clone(true)
    end if

    if content <> invalid
      selection.currentItem = content
      selection.initialItem = content

      'REALLY IMPORTANT: - we have a new type for deeplinking mappings on roku
      'these types map to screens.
      if deeplinkingData.itemType = "series"
        selection.action = "deeplinkSeries"
      else if deeplinkingData.action = "play"
        selection.action = "deeplinkPlay"
      else
        selection.action = "deeplinkDisplay"
      end if

      collection = createObject("roSGNode", "ZPEntry")
      collection.appendChild(content)
      collection.id = content.id
      selection.collection = collection
    end if
    return selection
  end function

  function exitFromDeeplinking()
    m.setInstance("deeplinkingSelection", invalid)
    zapp.appService.exitFromDeeplinking()
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Key handling
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  function onKeyPressBack() as boolean
    m.log.method("onKeyPressBack")

    m.togglePrehookController(false)
    m.dismissDeeplinkingController()

    return true
  end function

  private function onContentStateChange(state)
    m.log.info("DEEP LINK PARENT SCREEN STATE", state)
    m.toggleLoadingIndicator(state = "loading")
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Lifecycle methods
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  protected override function onShow()
    super.onShow()
    m.log.info("onShow - isShowingParentScreen", m.isShowingParentScreen)
    if m.isDeeplinkingApplied and not m.isShowingParentScreen
      m.log.info("the screen has appeared again, probably from a video player; but has no parent screen, exiting from deeplinking")
      m.exitFromDeeplinking()
    end if
  end function

  protected override function onGainedFocus(isSelfFocused as boolean)
    super.onGainedFocus(isSelfFocused)
    if isSelfFocused and m.currentScreen <> invalid
      m.setFocus(m.currentScreen)
    end if
  end function

end class