import "pkg:/source/authorization/BaseAuthPlugin.bs"
import "pkg:/source/lib/Request.bs"
import "pkg:/source/lib/URI.bs"
import "pkg:/source/lib/APIUtils.bs"
import "pkg:/source/authorization/BaseAuthPlugin.bs"

class InPlayerAuthPlugin extends zapp.auth.BaseAuthPlugin

  public apiUrl = "" ' note this is used by APIMixin
  private clientId as string

  public function new(plugin)
    super("InPlayerAuthPlugin", plugin)
    m.clientId = plugin.getStringValue("client_id")

    if plugin.getStringValue("environment", "prod") = "prod"
      m.apiUrl = "https://services.inplayer.com"
    else
      m.apiUrl = "https://staging-v2.inplayer.com"
    end if
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Overridden
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  public override function _getAccountInfo()
    'thankfully inplayer's default account json blob matches our own
    return m.authData.user
  end function

  public override function _verify()
    m.log.method("_verify")

    result = invalid

    if not m.isAuthDataExpired()
      params = {
        "grant_type": "refresh_token"
        "client_id": m.clientId
        "refresh_token": m.authData.refreshToken
      }

      m.log.info("using args", params)
      result = m.executeAPIRequest("FORMPOST", "accounts/authenticate", params)

      if result <> invalid and result.access_token <> invalid
        m.log.info("verify successful")

        return m.createAuthData("valid", result.access_token, result.access_token, result.refresh_token, result, result.expires, 200, "", result.account)
      end if
    else
      m.log.warn("Plugin has no valid auth data; could not complete verify")
    end if

    m.log.error("verify failed")
    return m.createErrorFromResult(result)
  end function

  public override function _register(credentials)
    m.log.method("_register")

    params = {
      "grant_type": "password"
      "type": "consumer"
      "client_id": m.clientId
      "full_name": credentials.name
      "username": credentials.username
      "password": credentials.password
      "metadata[surname]": "dummy" 'workaround for sudden requirement to provide surname on backend
      "password_confirmation": credentials.passwordConfirmation
    }

    m.log.info("using args", params)
    result = m.executeAPIRequest("FORMPOST", "accounts", params)

    if result <> invalid and result.access_token <> invalid
      m.log.info("register successful")
      return m.createAuthData("valid", result.access_token, result.access_token, result.refresh_token, result, result.expires, 200, "", result.account)
    end if

    m.log.error("registration failed")
    return m.createErrorFromResult(result)
  end function

  public override function _login(credentials)
    m.log.method("_login")

    params = {
      "grant_type": "password"
      "client_id": m.clientId
      "username": credentials.username
      "password": credentials.password
    }
    m.log.info("using args", params)
    result = m.executeAPIRequest("FORMPOST", "accounts/authenticate", params)

    if result <> invalid and result.access_token <> invalid
      m.log.info("login successful")
      return m.createAuthData("valid", result.access_token, result.access_token, result.refresh_token, result, result.expires, 200, "", result.account)
    end if

    m.log.error("login failed")
    return m.createErrorFromResult(result)
  end function

  public override function _logOut()
    m.loadAuthDataFromRegistry()
    if m.authData = invalid
      m.log.error("cannot log out - not logged in!")
      return m.createErrorResult("cannot log out: not logged in")
    end if

    result = m.executeAPIRequest("GET", "accounts/logout", invalid, true, m.authData.accessToken)

    if mc.getPath(result, "code", -1) = 200
      return m.createAuthData()
    else
      return m.createErrorFromResult(result)
    end if
  end function

  public override function _forgotPassword(credentials)
    params = {
      "merchant_uuid": m.clientId
      "email": credentials.username
    }

    result = m.executeAPIRequest("FORMPOST", "/accounts/forgot-password", params)

    if mc.getPath(result, "code", -1) = 200
      return m.createAuthData("valid")
    else
      return m.createErrorFromResult(result)
    end if
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Private Methods
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private function createErrorFromResult(result)
    m.log.method("trying to get error from", result)
    if result = invalid
      result = {}
    end if

    if result.errors <> invalid and result.errors.count() > 0
      message = result.errors[result.errors.keys()[0]]
    else
      message = mc.getPath(result, "message", "Unknown error")
    end if

    code = mc.getPath(result, "code", -1)
    return m.createErrorResult(message, code, result)
  end function

end class
