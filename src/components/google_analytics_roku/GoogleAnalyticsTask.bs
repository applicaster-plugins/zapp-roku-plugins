import "pkg:/source/maestro/lib/ThreadUtils.brs"
import "googleAnalytics.brs"

function init()
  print "GampTask.init"
  m.top.functionName = "execGampLoop"
end function

function initializeGA()
  gamobile_tracking_ids = [m.top.trackingId] ' tracking id for this channel
  device = createObject("roDeviceInfo")
  gamobile_client_id = m.top.clientId
  if gamobile_client_id = invalid or gamobile_client_id = ""
    gamobile_client_id = device.GetRandomUUID()
  end if
  gamobile_custom_fileds = m.top.customSessionParams

  ' Init analytics
  initGAMobile(gamobile_tracking_ids, gamobile_client_id, gamobile_custom_fileds)
  enableGAMobile(true)

  ' Enable or disable debug here for viewing events on the console
  setGADebug(true)
end function

function execGampLoop()
  print "GampTask.execGampLoop"
  port = CreateObject("roMessagePort")
  m.top.observeField("pageView", port)
  m.top.observeField("event", port)
  m.top.observeField("screenView", port)
  m.top.observeField("transaction", port)
  m.top.observeField("exception", port)
  m.top.observeField("trackingId", port)
  m.top.observeField("clientId", port)
  m.top.observeField("customSessionParams", port)

  initializeGA()

  ' Apps which use this long-lived task should delay any writes to the reporting fields (pageView, etc.)
  ' until this is set to true.  This is because there is a non-zero time from setting control = "RUN"
  ' until the time when the reporting fields are being observed.
  m.top.ready = true

  while true
    msg = MTU_waitPort(0, port)
    if type(msg) = "roSGNodeEvent" then
      field = msg.getField()
      data = msg.getData()
      if field = "pageView" then
        if data.hostname = invalid then data.hostname = ""
        if data.page = invalid then data.page = ""
        if data.title = invalid then data.title = ""
        gamobilePageView(data.hostname, data.page, data.title)
      else if field = "event" then
        if data.category = invalid then data.category = ""
        if data.action = invalid then data.action = ""
        if data.label = invalid then data.label = ""
        if data.value = invalid then data.value = ""
        gamobileEvent(data.category, data.action, data.label, data.value)
      else if field = "screenView" then
        if data.screenName = invalid then data.screenName = ""
        gamobileScreenView(data.screenName)
      else if field = "transaction" then
        if data.transactionId = invalid then data.transactionId = ""
        if data.affiliation = invalid then data.affiliation = ""
        if data.revenue = invalid then data.revenue = ""
        if data.shipping = invalid then data.shipping = ""
        if data.tax = invalid then data.tax = ""
        gamobileTransaction(data.transactionId, data.affiliation, data.revenue, data.shipping, data.tax)
      else if field = "exception" then
        if data.description = invalid then data.description = ""
        gamobileException(data.description)
      else if field = "trackingId" then
        initializeGA()
      else if field = "clientId" then
        initializeGA()
      else if field = "customSessionParams" then
        initializeGA()
      end if
    end if
  end while

end function
